;;; Rye Standard Library - Looping Macros

(module looping
  (export while while-r until for for-r)

  (import higher-order)
  (import list-core)
  (import predicates)

  (defmacro while (test . body)
    "Repeat body while test is truthy."
    (begin
      (define loop (gensym "while"))
      `(begin
         (define ,loop
           (lambda ()
           "Run loop body while test is true."
             (if ,test
               (begin ,@body (,loop))
               #nil)))
         (,loop))))

  (defmacro until (test . body)
    "Repeat body until test is truthy."
    `(while (not ,test) ,@body))

  (defmacro for (binding . body)
    "Map body over seq, binding var."
    (begin
      (define var (car binding))
      (define seq (car (cdr binding)))
      `(map (lambda (,var) (begin ,@body)) ,seq)))

  (defmacro while-r (test . body)
    "Execute an R while loop in the current environment."
    (begin
      (define body-expr
        (if (null? body)
          #nil
          (if (= (length body) 1)
            (car body)
            `({ ,@body))))
      `(r/eval (call (list 'while ',test ',body-expr)) (current-env))))

  (defmacro for-r (binding . body)
    "Execute an R for loop in the current environment."
    (begin
      (define var (car binding))
      (define seq (car (cdr binding)))
      (define body-expr
        (if (null? body)
          #nil
          (if (= (length body) 1)
            (car body)
            `({ ,@body))))
      `(r/eval (call (list 'for ',var ',seq ',body-expr)) (current-env))))
  )
