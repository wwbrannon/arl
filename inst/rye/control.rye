;;; Rye Standard Library - Control Flow Macros

(module control
  (export when unless and or cond case)

  (import predicates)
  (import list)

;;; Control flow macros

(defmacro when (test body)
  "Evaluate body when test is truthy."
  `(if ,test ,body #nil))

(defmacro unless (test body)
  "Evaluate body when test is falsy."
  `(if ,test #nil ,body))

;;; Boolean macros with short-circuit evaluation

(defmacro and (first . rest)
  "Short-circuit boolean AND."
  (if (null? rest)
    first
    `((lambda (tmp)
        (if tmp (and ,@rest) tmp))
      ,first)))

(defmacro or (first . rest)
  "Short-circuit boolean OR."
  (if (null? rest)
    first
    `((lambda (tmp)
        (if tmp tmp (or ,@rest)))
      ,first)))

;;; Conditional macros

(defmacro cond (clause . rest)
  "Multi-branch conditional."
  (if (null? clause)
    #nil
    (begin
      (define test (car clause))
      (define body (cdr clause))
      (if (and (symbol? test) (= (as.character test) "else"))
        `(begin ,@body)
        `(if ,test (begin ,@body) (cond ,@rest))))))

(defmacro case (key clause . rest)
  "Branch on key equality."
  (if (null? clause)
    #nil
    (begin
      (define datum (car clause))
      (define body (cdr clause))
      (if (and (symbol? datum) (= (as.character datum) "else"))
        `(begin ,@body)
        `(if (= ,key ,datum) (begin ,@body) (case ,key ,@rest))))))
)
