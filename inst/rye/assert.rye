;;; Rye Standard Library - Assertion Helpers

(module assert
  (export assert assert-equal assert-true assert-false assert-eq assert-error)

  (import core)
  (import equality)
  (import strings)
  (import dict)

;;' @section Assertion Helpers
;;' These functions provide test-style assertions that signal errors on failure
;;' and return `#t` on success.

  ;;' @examples
  ;;' (assert #t)               ; => #t
  ;;' (assert (> 3 2))          ; => #t
  ;;' (assert #f "must be true")  ; signals "must be true"
  ;;' (assert #f)               ; signals "Assertion failed"
  ;;' @seealso error
  (define assert
    (lambda (cond (msg "Assertion failed"))
      "Assert condition or raise error."
      (if cond
        #t
        (error msg))))

  ;;' @examples
  ;;' (assert-equal 3 (+ 1 2))  ; => #t
  ;;' (assert-equal "a" "a")    ; => #t
  ;;' (assert-equal 1 2)        ; signals error
  ;;' @seealso assert-eq, assert-true
  (define assert-equal
    (lambda (expected actual)
      "Assert expected and actual are equal?."
      (if (not (equal? expected actual))
        (begin
          (define error-msg (string-append "Expected: " (->string expected) ", Got: " (->string actual)))
          (error error-msg))
        #t)))

  ;;' @examples
  ;;' (assert-true #t)          ; => #t
  ;;' (assert-true 1)           ; => #t
  ;;' (assert-true #f)          ; signals error
  ;;' @seealso assert-false, assert
  (define assert-true
    (lambda (value)
      "Assert value is truthy."
      (if (not value)
        (begin
          (define error-msg (string-append "Expected truthy value, got: " (->string value)))
          (error error-msg))
        #t)))

  ;;' @examples
  ;;' (assert-false #f)         ; => #t
  ;;' (assert-false #nil)       ; => #t
  ;;' (assert-false #t)         ; signals error
  ;;' @seealso assert-true, assert
  (define assert-false
    (lambda (value)
      "Assert value is falsy."
      (if value
        (begin
          (define error-msg (string-append "Expected falsy value, got: " (->string value)))
          (error error-msg))
        #t)))

  ;;' @examples
  ;;' (define x '(1 2 3))
  ;;' (assert-eq x x)           ; => #t (same object)
  ;;' (assert-eq '(1) '(1))     ; signals error (not identical)
  ;;' @note Uses `identical?` (pointer equality), not `equal?` (structural equality). Use `assert-equal` for value comparison.
  ;;' @seealso assert-equal, assert
  (define assert-eq
    (lambda (expected actual)
      "Assert expected and actual are identical?."
      (if (not (identical? expected actual))
        (begin
          (define error-msg (string-append "Expected (identical?): " (->string expected) ", Got: " (->string actual)))
          (error error-msg))
        #t)))

  ;;' @examples
  ;;' (assert-error (lambda () (error "boom")))   ; => #t
  ;;' (assert-error (lambda () (/ 1 0)))           ; => #t
  ;;' (assert-error (lambda () 42))                ; signals error
  ;;' @seealso assert, error
  (define assert-error
    (lambda (thunk)
      "Assert thunk throws an error."
      ;; Use a unique sentinel value to detect if error was caught
      (define error-caught (gensym "error-caught"))
      (define thunk-expr (call (list thunk)))
      (define result
        (r/eval (call (list 'tryCatch thunk-expr
                            :error (lambda (e) error-caught)))
                (current-env)))
      (if (not (identical? result error-caught))
        (error "Expected an error to be thrown, but none was")
        #t)))
)
