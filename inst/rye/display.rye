;;; Rye Standard Library - Display Helpers

(module display
  (export __format-atomic __format-seq format-value display println str trace)

  (import list)
  (import strings)
  (import control)
  (import dict)
  (import set)

  ;; Format atomic values to a single string.
  (define __format-atomic
    (lambda (x)
      (define parts (as.character x))
      (if (= (length parts) 0)
        ""
        (if (= (length parts) 1)
          (as.character parts)
          (string-join parts " ")))))

  ;; Format list/call contents with spaces.
  (define __format-seq
    (lambda (items)
      (define parts (lapply (__as-list items) format-value))
      (if (= (length parts) 0)
        ""
        (string-join parts " "))))

  ;; Format functions/macros/closures.
  (define __format-function
    (lambda (x)
      (if (r/call "inherits" (list x "rye_closure"))
        "<closure>"
        (if (r/call "is.function" (list x))
          (if (r/call "is.null" (list (r/call "attr" (list x "rye_macro" :exact #t))))
            "<function>"
            "<macro>")
          #nil))))

  ;; Format environments (excluding dict, set, promise).
  (define __format-environment
    (lambda (x)
      (if (and (r/call "is.environment" (list x))
               (not (dict? x))
               (not (set? x))
               (not (promise? x)))
        (begin
          (define classes (r/call "class" (list x)))
          (define class-str (if (> (length classes) 0)
                              (r/call "paste" (list classes :collapse ", "))
                              "environment"))
          (string-join (list "<" class-str ">") ""))
        #nil)))

  ;;' @description Format value for display.
  (define format-value
    (lambda (x)
      (define fn-tag (__format-function x))
      (if (not (is.null fn-tag))
        fn-tag
        (begin
          ;; Check dotted pair before environment
          ;; (R6 instances are environments in R)
          (if (pair? x)
              (begin
                (define parts (__cons-parts x))
                (define prefix (r/call "[[" (list parts "prefix")))
                (define tail (r/call "[[" (list parts "tail")))
                (define head-str (if (= (length prefix) 0)
                  ""
                  (string-join (lapply prefix format-value) " ")))
                (define tail-str (format-value tail))
                (string-join (list "(" head-str " . " tail-str ")") ""))
              (begin
                (define env-tag (__format-environment x))
                (if (not (is.null env-tag))
                  env-tag
                  (if (values? x)
                (begin
                  (define inner (r/call "unclass" (list x)))
                  (if (= (length inner) 0)
                    "(values)"
                    (string-join (append (list "(values") (lapply inner format-value) (list ")")) " ")))
                (if (dict? x)
                  (begin
                    (define keys (dict-keys x))
                    (define values (lapply keys (lambda (k) (dict-get x k))))
                    (__format-seq values))
                  (if (set? x)
                    (__format-seq (r/call "as.list" (list x :all.names #t)))
                    (if (promise? x)
                      "<promise>"
                      (if (is.symbol x)
                        (as.character x)
                        (if (or (is.call x) (is.list x))
                          (__format-seq x)
                          (__format-atomic x))))))))))))))

  ;;' @section Display and Output
  ;;' Functions for formatting and printing values.

  ;;' @description Print value with newline.
  ;;' @examples
  ;;' (display 42)             ; prints "42" with newline
  ;;' (display "hello")        ; prints "hello" with newline
  ;;' (display (list 1 2 3))   ; prints "1 2 3" with newline
  ;;' @seealso println, trace
  (define display
    (lambda (x)
      (define quiet (r/call "Sys.getenv" (list "RYE_QUIET" "")))
      (if (= quiet "")
        (r/call "cat" (list (format-value x) "\n"))
        #nil)))

  ;;' @description Alias for display.
  ;;' @seealso display
  (define println
    (lambda (x)
      (display x)))

  ;;' @description Concatenate formatted values into string.
  ;;' @examples
  ;;' (str "hello" " " "world")  ; => "hello world"
  ;;' (str 1 "+" 2)              ; => "1+2"
  ;;' @seealso string-join, format
  (define str
    (lambda (. args)
      (define parts (lapply (__as-list args) format-value))
      (if (= (length parts) 0)
        ""
        (string-join parts ""))))

  ;;' @description Print value and return it.
  ;;' @examples
  ;;' (trace 42)               ; prints "42", returns 42
  ;;' (trace 42 "result")      ; prints "result: 42", returns 42
  ;;' @seealso warn, display
  (define trace
    (lambda (x . rest)
      (define label (if (> (length rest) 0) (car rest) #nil))
      (define quiet (r/call "Sys.getenv" (list "RYE_QUIET" "")))
      (if (= quiet "")
        (begin
          (if (is.null label)
            #nil
            (r/call "cat" (list (format-value label) ": ")))
          (r/call "cat" (list (format-value x) "\n"))
          x)
        x)))
)
