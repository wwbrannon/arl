;;; Rye Standard Library - Display Helpers

(module display
  (export __format-atomic __format-seq format-value display println str trace)

  (import list)
  (import strings)
  (import control)
  (import dict)
  (import set)

  (define __format-atomic
    (lambda (x)
      "Format atomic values to a single string."
      (define parts (as.character x))
      (if (= (length parts) 0)
        ""
        (if (= (length parts) 1)
          (as.character parts)
          (string-join parts " ")))))

  (define __format-seq
    (lambda (items)
      "Format list/call contents with spaces."
      (define parts (lapply (__as-list items) format-value))
      (if (= (length parts) 0)
        ""
        (string-join parts " "))))

  (define __format-function
    (lambda (x)
      "Format functions/macros/closures."
      (if (r/call "inherits" (list x "rye_closure"))
        "<closure>"
        (if (r/call "is.function" (list x))
          (if (r/call "is.null" (list (r/call "attr" (list x "rye_macro" :exact #t))))
            "<function>"
            "<macro>")
          #nil))))

  (define __format-environment
    (lambda (x)
      "Format environments (excluding dict, set, promise)."
      (if (and (r/call "is.environment" (list x))
               (not (dict? x))
               (not (set? x))
               (not (promise? x)))
        (begin
          (define classes (r/call "class" (list x)))
          (define class-str (if (> (length classes) 0)
                              (r/call "paste" (list classes :collapse ", "))
                              "environment"))
          (string-join (list "<" class-str ">") ""))
        #nil)))

  (define format-value
    (lambda (x)
      "Format value for display."
      (define fn-tag (__format-function x))
      (if (not (is.null fn-tag))
        fn-tag
        (begin
          ;; Check dotted pair before environment (R6 instances are environments in R)
          (if (pair? x)
              (begin
                (define parts (__cons-parts x))
                (define prefix (r/call "[[" (list parts "prefix")))
                (define tail (r/call "[[" (list parts "tail")))
                (define head-str (if (= (length prefix) 0)
                  ""
                  (string-join (lapply prefix format-value) " ")))
                (define tail-str (format-value tail))
                (string-join (list "(" head-str " . " tail-str ")") ""))
              (begin
                (define env-tag (__format-environment x))
                (if (not (is.null env-tag))
                  env-tag
                  (if (values? x)
                (begin
                  (define inner (r/call "unclass" (list x)))
                  (if (= (length inner) 0)
                    "(values)"
                    (string-join (append (list "(values") (lapply inner format-value) (list ")")) " ")))
                (if (dict? x)
                  (begin
                    (define keys (dict-keys x))
                    (define values (lapply keys (lambda (k) (dict-get x k))))
                    (__format-seq values))
                  (if (set? x)
                    (__format-seq (r/call "as.list" (list x :all.names #t)))
                    (if (promise? x)
                      "<promise>"
                      (if (is.symbol x)
                        (as.character x)
                        (if (or (is.call x) (is.list x))
                          (__format-seq x)
                          (__format-atomic x))))))))))))))

  (define display
    (lambda (x)
      "Print value with newline."
      (define quiet (r/call "Sys.getenv" (list "RYE_QUIET" "")))
      (if (= quiet "")
        (r/call "cat" (list (format-value x) "\n"))
        #nil)))

  (define println
    (lambda (x)
      "Alias for display."
      (display x)))

  (define str
    (lambda (. args)
      "Concatenate formatted values into string."
      (define parts (lapply (__as-list args) format-value))
      (if (= (length parts) 0)
        (character 0)
        (string-join parts ""))))

  (define trace
    (lambda (x . rest)
      "Print value and return it."
      (define label (if (> (length rest) 0) (car rest) #nil))
      (define quiet (r/call "Sys.getenv" (list "RYE_QUIET" "")))
      (if (= quiet "")
        (begin
          (if (is.null label)
            #nil
            (r/call "cat" (list (format-value label) ": ")))
          (r/call "cat" (list (format-value x) "\n"))
          x)
        x)))
)
