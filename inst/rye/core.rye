;;; Rye Standard Library - Core Functions

(module core
  (export = error warn assert identity values values? call-with-values call/cc call-with-current-continuation)

  (define =
    (lambda (x y)
      "Equality helper using R ==."
      (== x y)))

  (define error
    (lambda (msg)
      "Signal an error with message."
      (stop msg :call. #f)))

  (define warn
    (lambda (msg)
      "Emit warning with message."
      (warning msg :call. #f)))

  (define assert
    (lambda (cond (msg "Assertion failed"))
      "Assert condition or raise error."
      (if cond
        #t
        (error msg))))

  (define identity
    (lambda (x)
      "Return argument."
      x))

  (define values
    (lambda (. args)
      "Return multiple values to a call-with-values consumer."
      (r/call "structure" (list args :class "rye_values"))))

  (define values?
    (lambda (x)
      "Return #t if x is a multiple-values container."
      (if (is.list x)
        (isTRUE (inherits x "rye_values"))
        #f)))

  (define call-with-values
    (lambda (producer consumer)
      "Call producer and pass its values to consumer."
      (if (not (is.function producer))
        (stop "call-with-values expects a function as the producer"))
      (if (not (is.function consumer))
        (stop "call-with-values expects a function as the consumer"))
      (define produced (producer))
      (define args
        (if (values? produced)
          (r/call "unclass" (list produced))
          (list produced)))
      (apply consumer args)))

  (define call/cc callCC)
  (define call-with-current-continuation callCC)
)
