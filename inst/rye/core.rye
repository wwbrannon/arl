;;; Rye Standard Library - Core Functions

(module core
  (export error warn
            identity values values? call-with-values call/cc call-with-current-continuation doc! doc
            funcall r/call get license)

  (import _r)

  ;; Documentation helpers
  (defmacro doc! (sym docstring)
    "Attach docstring to function. Usage: (doc! function-name \"docstring\")"
    `(set! ,sym (rye_set_doc ,sym ,docstring)))

  (define doc
    (lambda (fn)
      "Retrieve docstring from function's rye_doc attribute."
      (rye_get_doc fn)))

  (define funcall
    (lambda (fn args)
      "Apply a function with a provided list of arguments."
      (do.call fn args :quote #t)))

  (define r/call
    (lambda (fn (args (list)) (envir (globalenv)))
      "Call an R function with optional environment. Searches from .GlobalEnv by default, finding base and loaded package functions."
      (define fn-name
        (if (is.symbol fn)
          (as.character fn)
          (if (is.character fn)
            fn
            (error "r/call requires a symbol or string function name"))))
      (define fn-obj (__rget fn-name :envir envir :inherits #t))
      (funcall fn-obj (as.list args))))  ; as.list handles environments

  (define get
    (lambda (name (envir (globalenv)) (inherits #t))
      "Get a binding by name, defaulting to .GlobalEnv."
      (define name-str
        (if (is.symbol name)
          (as.character name)
          name))
      (r/call "get" (list name-str :envir envir :inherits inherits))))

  (define error
    (lambda (msg)
      "Signal an error with message."
      (stop msg :call. #f)))

  (define warn
    (lambda (msg)
      "Emit warning with message."
      (warning msg :call. #f)))

  (define identity
    (lambda (x)
      "Return argument."
      x))

  (define values
    (lambda (. args)
      "Return multiple values to a call-with-values consumer."
      (r/call "structure" (list args :class "rye_values"))))

  (define values?
    (lambda (x)
      "Return #t if x is a multiple-values container."
      (if (is.list x)
        (isTRUE (inherits x "rye_values"))
        #f)))

  (define call-with-values
    (lambda (producer consumer)
      "Call producer and pass its values to consumer."
      (if (not (is.function producer))
        (stop "call-with-values expects a function as the producer"))
      (if (not (is.function consumer))
        (stop "call-with-values expects a function as the consumer"))
      (define produced (producer))
      (define args
        (if (values? produced)
          (r/call "unclass" (list produced))
          (list produced)))
      (funcall consumer args)))

  (define call/cc callCC)
  (define call-with-current-continuation callCC)

  (define license
    (lambda ()
      "Display Rye and R license information."
      (begin
        (r/call "cat" (list "Rye is licensed under the MIT License:\n\n"))
        (define license-file (r/call "system.file" (list "LICENSE.md" :package "rye")))
        (define license-lines (r/call "readLines" (list license-file)))
        (r/call "cat" (list license-lines :sep "\n"))
        (r/call "cat" (list "\n\nRye is built on R. R's license:\n\n"))
        (r/call "license" (list)))))
)
